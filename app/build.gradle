apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 24
    buildToolsVersion '24'

    defaultConfig {
        applicationId "arun.com.chromer"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 22
        versionName "1.6 B3.1"

        buildConfigField "String", "BASE_64", getBase64()
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Chromer"
            lintOptions {
                disable 'MissingTranslation'
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '- Debug'
            resValue "string", "app_name", "Chromer - Debug"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    // Support
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:design:24.1.1'
    compile 'com.android.support:customtabs:24.1.1'
    compile 'com.android.support:cardview-v7:24.1.1'
    compile 'com.android.support:preference-v14:24.1.1'
    compile 'com.android.support:palette-v7:24.1.1'
    // ----

    compile 'com.github.paolorotolo:appintro:4.0.0'

    compile 'com.mikepenz:community-material-typeface:1.5.54.2@aar'
    compile('com.mikepenz:materialdrawer:5.3.6@aar') {
        transitive = true
    }

    compile 'de.psdev.licensesdialog:licensesdialog:1.8.1'

    compile 'com.github.afollestad.material-dialogs:core:0.8.6.1'
    compile 'com.github.afollestad.material-dialogs:commons:0.8.6.1'

    compile 'com.jakewharton.timber:timber:4.1.2'
    compile 'com.jakewharton:butterknife:8.2.1'
    apt 'com.jakewharton:butterknife-compiler:8.2.1'

    compile 'com.github.satyan:sugar:1.5'
    compile 'com.github.bumptech.glide:glide:3.7.0'

    compile 'com.flipboard:bottomsheet-core:1.5.1'
    compile 'com.flipboard:bottomsheet-commons:1.5.1'

    compile 'com.facebook.rebound:rebound:0.3.8'

    compile 'org.jsoup:jsoup:1.9.2'

    compile 'com.github.nekocode:Badge:1.2'
}

def getBase64() {
    try {
        Properties properties = new Properties()
        properties.load(new FileInputStream("values.properties"))
        return "\"" + properties.getProperty("BASE_64") + "\""
    } catch (Exception e) {
        return ""
    }
}